#----------------------------------------------------------------
# Functon support OPEN Dev USB GPIO EXTENDER TOIC
# by Sertik version 1.0 08/10/2024
#----------------------------------------------------------------
# check in ROS 6.49.10

# usage: 

# $fOpenDevExtTOIC list - output a list of function commands for the module to the terminal and log
# :put [$fOpenDevExtTOIC logic] - return the set value of the operating logic
# $fOpenDevExtTOIC logic [true/false] - set forward/reverse operating logic
# $fOpenDevExtTOIC OutOn - turn on OUT (2)
# $fOpenDevExtTOIC OutOff - turn off OUT (2)
# $fOpenDevExtTOIC In - read IN (6)
# $fOpenDevExtTOIC SPI xxxxx - send xxxxx to SPI and return module`s answer
# $fOpenDevExtTOIC PWM - read PWM-OUT
# $fOpenDevExtTOIC AT - check connect and LED
# $fOpenDevExtTOIC ATI - request information about the firmware version

# module commands:
# -----------------------------------------------------------------------
# S sets the output (2) to 1.
# R sets the output (2) to 0.
# G read the current value of input (6)
# A - read ADC
# K - send data to SPI and take answer
# F - read data in PWM
# L - test connect
# I request information about the firmware version.


:global  fOpenDevExtTOIC do={
 
:local ModuleType "USB GPIO EXTENDER TOIC"
:local portTypeUSB "usb"
:global OpenDevModuleType $ModuleType
:local USBresLinuxName "TOIC-F0-GE"
:global OpenDevReleLogic
:local UsbGpioExtFlag false
:local portUSB;
:local BaudRate 9600
:local DataBits 8
:local Parity none
:local StopBits 1
:local FlowControl none
:local PppclientName $ModuleType

:local ArrayCom {
   "logic"="X"
   "OutOn"="S"
   "OutOff"="R"
   "In"="G"
   "ADC"="A"
   "PWM"="F"
   "SPI"="K"
   "ATI"="I"
   "AT"="L"
 }

:if ([:len $1]=0) do={:return "Еrror: no set name command"}

# help
   :if ($1="help") do={
      :put ""; :put "---- Function support for $OpenDevModuleType ----"
                   :put "      version $version"
                   :put  " usage:" 
:terminal style "syntax-meta"
:put "$0 help" 
:put "$0 list"
:put "$0 logic [true/false]"
:put "$0 reset"
:put "$0 SetOut XXYXY [0-1]"
:put "$0 OutOn X [1-5]"
:put "$0 OutOff Y [1-5]"
:terminal style none
  :return []}

# list 
   :if ($1="list") do={
      :put ""; :put "<---- Supported $OpenDevModuleType commands  ---->"
          :foreach k,v in $ArrayCom do={:put ("  "."$k")}
  :return []}

# logic
   :if ($1="logic") do={
       :if (($2="true") or ($2="false")) do={
           :if ($2="true") do={:set OpenDevReleLogic true}
           :if ($2="false") do={:set OpenDevReleLogic false}
           :return OK
     } else={:if ([:len $2]=0) do={
         :if (($OpenDevReleLogic=true) or ($OpenDevReleLogic=false)) do={
         :return $OpenDevReleLogic} else={:return "logic is not specified or incorrect"}
                 }
    :return ("Error"." $0"." $1")}
}

local UsbGpioExtName
:do {
:set UsbGpioExtName [/system resource usb get [/system resource usb find name~$USBresLinuxName] name]
    } on-error={}
:if ($UsbGpioExtName=$USBresLinuxName) do={:set UsbGpioExtFlag true}
:if ($UsbGpioExtFlag=false) do={:return "Error: Not find $OpenDevModuleType module in ROS system. Please, check device in USB port"}


:global ODUsbGPIOExtPort
:local NewPort
:local NowPort $ODUsbGPIOExtPort; # сохранить текущий порт

:do {
    :foreach portId in=[/port find name~$portTypeUSB !inactive] do={:set portUSB ([/port get $portId]->"name")}
    } on-error={}  

    :set NewPort $portUSB

    :if (([:len $NewPort]=0) or ([:len [/port find name=$NewPort]]=0)) do={:return "Error: Not find port for $OpenDevModuleType module, port inactive or busy. Please, check /port"}

   :if (($NowPort!=$NewPort) and ([/port find name=$NowPort and inactive=yes])) do={:set ODUsbGPIOExtPort $NewPort} else={:set ODUsbGPIOExtPort $NowPort}
   :if ([:len $ODUsbGPIOExtPort]=0) do={:set ODUsbGPIOExtPort $NewPort}
  
  :local consoleFlagOff false
        if ([:len [/system console find port=$ODUsbGPIOExtPort and !disabled]]>0) do={
                :set consoleFlagOff true
                /system console set [/system console find port=$ODUsbGPIOExtPort] disable=yes
        }

    do {
             /port set [/port find name=$ODUsbGPIOExtPort] baud-rate=$BaudRate data-bits=$DataBits parity=$Parity stop-bits=$StopBits flow-control=$FlowControl
          } on-error={:return "Error set port $ODUsbGPIOExtPort. Function $0 d`not work"}


# main function`s code
   :local cmd ($ArrayCom->$1)
    :if ([:len $cmd]=0) do={:return "Error: bad command"}
               :put "Execute command $OpenDevModuleType: $1 $2"
               :log warning "Execute command $OpenDevModuleType: $1 $2"

:if ([:len $OpenDevReleLogic]=0) do={:set OpenDevReleLogic true}
:if (($1="OutOn") && ($OpenDevReleLogic=false)) do={:set cmd ($ArrayCom->"OutOff")}
:if (($1="OutOff") && ($OpenDevReleLogic=false)) do={:set cmd ($ArrayCom->"OutOn")}

:if ([/interface ppp-client find name=$PppclientName]) do={/interface ppp-client remove [/interface ppp-client find name=$PppclientName]}

# :put ("Send module "."$USBresLinuxName "."command: "."$cmd"."$2")

     /interface ppp-client add name=$PppclientName dial-on-demand=no port=$ODUsbGPIOExtPort null-modem=yes disabled=yes
     :delay 1s
     :local GPIOanswer [/interface ppp-client at-chat $PppclientName input=("$cmd"."$2") as-value]
     /interface ppp-client remove [/interface ppp-client find name=$PppclientName]

   :if ($consoleFlagOff) do={
   :do {/system console set [/system console find port=$ODUsbGPIOExtPort] disable=no} on-error={}
     }
# end	 
   :return $GPIOanswer
}
